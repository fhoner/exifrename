plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'com.peterabeles.gversion' version '1.10'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.encoding = "UTF-8"
}

mainClassName = 'com.fhoner.exifrename.renameui.Main'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    implementation project(":core")
    implementation("org.apache.logging.log4j:log4j-core:2.17.0")
    compileOnly("org.projectlombok:lombok:1.18.22")
    annotationProcessor("org.projectlombok:lombok:1.18.22")
    implementation("com.google.inject:guice:5.0.1")
    runtimeOnly("org.openjfx:javafx-graphics:$javafx.version:win")
    runtimeOnly("org.openjfx:javafx-graphics:$javafx.version:linux")
    runtimeOnly("org.openjfx:javafx-graphics:$javafx.version:mac")
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'com.fhoner.exifrename.renameui.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

gversion {
    srcDir = "src/main/java/"
    classPackage = "com.fhoner.exifrename.renameui"
    className = "RenameUIVersion"
}

project.compileJava.dependsOn(createVersionFile)
